# -*- mode: python -*-
Import('env')

env.Tar(env['TARBALL'], ['SConscript', 'config', 'lib'])

for d in ('filtergen', 'fg-parse.dg', 'fg-scan.dg', 'ipts-parse.dg', 'ipts-scan.dg'):
  # Exclude log and sum which are generated files when running the suite.
  env.Tar(env['TARBALL'], [f for f in env.Glob(d + '/*') if not f.path.endswith('.log') and not f.path.endswith('.sum')])

f = env.DejaGnu('filtergen')
env.Depends(f, '#filtergen')
fgp = env.DejaGnu('fg-parse')
env.Depends(fgp, '#input/filtergen/t/parse')
fgs = env.DejaGnu('fg-scan')
env.Depends(fgs, '#input/filtergen/t/scan')
iptsp = env.DejaGnu('ipts-parse')
env.Depends(iptsp, '#input/iptables-save/t/parse')
iptss = env.DejaGnu('ipts-scan')
env.Depends(iptss, '#input/iptables-save/t/scan')

suites = [f, fgp, fgs, iptsp, iptss]

env.Depends(suites, '#site.exp')
env.Depends(suites, env.Glob('config/*.exp'))
# This one's too broad; one lib per suite.
env.Depends(suites, env.Glob('lib/*.exp'))

env.Alias('check', suites)
env.AlwaysBuild(suites)
